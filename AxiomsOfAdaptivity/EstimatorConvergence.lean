import Mathlib
import AxiomsOfAdaptivity.Basics
import AxiomsOfAdaptivity.Summability

open Filter
open TopologicalSpace
open BigOperators
open Finset
open scoped Topology


-- Utils
lemma nnreal_fun_bbd_below (f : â„• â†’ NNReal) : BddBelow (Set.range f) := by {
  simp only [OrderBot.bddBelow]
}

lemma lift_bound_above (f : â„• â†’ NNReal) (hf : BddAbove (Set.range f)) : BddAbove (Set.range (Î» n â†¦ â†‘(f n) : â„• â†’ â„)) := by {
  exact BddAbove.range_comp hf fun â¦ƒa bâ¦„ a â†¦ a
}

lemma lift_bound_below (f : â„• â†’ NNReal) : BddBelow (Set.range (Î» n â†¦ â†‘(f n) : â„• â†’ â„)) := by {
  refine BddBelow.range_comp ?_ fun â¦ƒa bâ¦„ a â†¦ a
  exact nnreal_fun_bbd_below f
}

lemma nnreal_limsup_const_mul {u : â„• â†’ NNReal} {a : NNReal} (hu: IsBoundedUnder (Â· â‰¤ Â·) atTop u):
    Filter.limsup (fun n â†¦ a * u n) atTop = a * Filter.limsup u atTop := by {
  rw [â† ENNReal.coe_inj]
  push_cast
  rw [ENNReal.ofNNReal_limsup hu, ENNReal.ofNNReal_limsup]
  push_cast
  rw [ENNReal.limsup_const_mul_of_ne_top (by simp)]

  let f : NNReal â†’ NNReal := fun x â†¦ a * x
  have hf : Monotone f := by exact mul_left_mono
  exact Monotone.isBoundedUnder_le_comp hf hu
}

lemma smaller_q_eq_zero (a q: NNReal) (hq : q < 1) (ha : a â‰¤ q*a) : a = 0 := by {
  by_contra h
  have h' : a > 0 := by exact pos_of_ne_zero h
  nth_rw 1 [â† mul_one a] at ha
  rw [mul_comm, mul_le_mul_iff_of_pos_right h'] at ha

  have hc : Â¬q < 1 := by exact not_lt_of_ge ha
  contradiction
}

lemma monotone_map_bdd_above_range {h : NNReal â†’ NNReal} {f : â„• â†’ NNReal} (hh : Monotone h) (hf: BddAbove (Set.range f)) :
    BddAbove (Set.range (hâˆ˜f)) := by {
  rw [Set.range_comp]
  exact Monotone.map_bddAbove hh hf
}

-- limsup of unbounded functions is 0, but this is not troublesome
-- because the theorem we use to conclude the limit from the limsup
-- needs an additional proof of boundedness
example : limsup (Î» n : NNReal â†¦ n) atTop = 0 := by {
  refine NNReal.limsup_of_not_isBoundedUnder ?_
  refine Filter.not_isBoundedUnder_of_tendsto_atTop ?_
  exact fun _ a â†¦ a
}

-- 4.18
structure SimpleEstimatorReduction (Î· d : â„• â†’ NNReal) where
  q : NNReal
  q_range : q âˆˆ Set.Ioo 0 1
  C : NNReal
  C_pos : C > 0
  bound : âˆ€ n, (Î· (n + 1))^2 â‰¤ q * (Î· n)^2 + C * (d n)^2

namespace SimpleEstimatorReduction

variable {Î· d : â„• â†’ NNReal} (h : SimpleEstimatorReduction Î· d)
include h

def weightedSum (n : â„•) : NNReal :=
  âˆ‘ k âˆˆ Finset.range (n + 1), h.q ^ (n - k) * (d k)^2

def upperBound (n : â„•) : NNReal :=
  h.q ^ (n + 1) * (Î· 0)^2 + h.C * h.weightedSum n

lemma estimator_recursive_upper_bound (n : â„•) :
    (Î· (n+1))^2 â‰¤ h.upperBound n := by {
  induction' n with n ih
  Â· unfold upperBound weightedSum
    simp
    apply h.bound 0
  Â· calc  Î· (n + 1 + 1) ^ 2
      _ â‰¤ h.q * (Î· (n + 1))^2 + h.C * (d (n + 1))^2 := by apply h.bound
      _ â‰¤ h.q * h.upperBound n + h.C * (d (n + 1))^2 := by gcongr
      _ = h.upperBound (n+1) := by {
        -- TODO maybe split this up into smaller chunks
        unfold upperBound weightedSum
        nth_rw 2 [sum_range_succ]
        rw [mul_add, â† mul_assoc, â† pow_succ', â† mul_assoc, mul_comm h.q h.C, mul_assoc, mul_sum, mul_add]
        rw [Finset.sum_congr rfl fun k hk => by
          rw [â† mul_assoc, â† pow_succ', â† Nat.sub_add_comm (mem_range_succ_iff.mp hk)]]
        simp [pow_zero, add_assoc]
      }
}

lemma weighted_sum_bound (hd : BddAbove (Set.range d)) (n : â„•):
    h.weightedSum n â‰¤ (â¨† i, d i)^2 * (1/h.q) / (1/h.q - 1) := by {
  let âŸ¨q, q_range, C, C_pos, boundâŸ© := h
  unfold weightedSum

  have hqâ‚ : 1/q â‰¥ 1 := by {
    simp
    apply one_le_inv_iffâ‚€.mpr
    exact âŸ¨q_range.1, le_of_lt q_range.2âŸ©
  }
  have hqâ‚‚ : (1 / q) ^ (n + 1) â‰¥ 1 := one_le_powâ‚€ hqâ‚

  have hâ‚ : âˆ€ k, d k â‰¤ (â¨† i, d i) := by {
    intros k
    exact (le_ciSup_iff' hd).mpr fun b a â†¦ a k
  }

  have hâ‚‚ : âˆ‘ k âˆˆ (range (n + 1)), q^(n-k) = âˆ‘ k âˆˆ (range (n + 1)), q^n/q^k := by {
    apply Finset.sum_congr rfl
    intros k hk
    rw [â† NNReal.rpow_natCast]
    rw [Nat.cast_sub (mem_range_succ_iff.mp hk)]
    rw [NNReal.rpow_sub_natCast (ne_of_gt q_range.1)]
    simp
  }

  have hâ‚ƒ : âˆ‘ k âˆˆ range (n + 1), (1/q)^k = ((1/q)^(n+1) - 1)/(1/q - 1) := by {
    rw[â† NNReal.coe_inj]
    push_cast [hqâ‚, hqâ‚‚]
    apply geom_sum_eq
    Â· simp [ne_of_lt q_range.2]
  }

  have hâ‚„ : q^n * (1/q^(n+1) - 1)/(1/q - 1) = ((1/q) - q^n)/(1/q - 1) := by {
    rw [mul_tsub, mul_one, one_div]
    group
    rw [â† zpow_addâ‚€ (ne_of_gt q_range.1)]
    simp
  }

  have hâ‚… : (1/q) - q^n â‰¤ 1/q := by {
    have : q^n â‰¤ 1/q := by {
      trans 1
      Â· exact pow_le_oneâ‚€ (le_of_lt q_range.1) (le_of_lt q_range.2)
      Â· exact hqâ‚
    }
    rw [â† NNReal.coe_le_coe]
    push_cast [this]
    simp
  }

  calc âˆ‘ k âˆˆ (range (n + 1)), q^(n-k) * (d k)^2
    _ â‰¤ âˆ‘ k âˆˆ (range (n + 1)), q^(n-k) * (â¨† i, d i)^2 := by gcongr; apply hâ‚
    _ = âˆ‘ k âˆˆ (range (n + 1)), (â¨† i, d i)^2 * q^(n-k) := by simp_rw [mul_comm]
    _ = (â¨† i, d i)^2 * âˆ‘ k âˆˆ range (n + 1), q^(n-k) := by simp [mul_sum]
    _ = (â¨† i, d i)^2 * âˆ‘ k âˆˆ range (n + 1), q^n/q^k := by rw [hâ‚‚]
    _ = (â¨† i, d i)^2 * âˆ‘ k âˆˆ range (n + 1), q^n * (1/q)^k := by field_simp
    _ = (â¨† i, d i)^2 * q^n * âˆ‘ k âˆˆ range (n + 1), (1/q)^k := by simp [â† mul_sum, mul_assoc]
    _ = (â¨† i, d i)^2 * (q^n * (1/q^(n+1) - 1)/(1/q - 1)) := by rw [hâ‚ƒ]; field_simp [mul_assoc]
    _ = (â¨† i, d i)^2 * ((1/q) - q^n)/(1/q - 1) := by rw [hâ‚„, â† mul_div_assoc']
    _ â‰¤ (â¨† i, d i)^2 * (1/q)/(1/q - 1) := by gcongr
}

lemma estimator_bounded (hd : BddAbove (Set.range d)) : BddAbove (Set.range Î·) := by {
  let K := ((Î· 0)^2 + h.C * ((â¨† i, d i)^2 * (1/h.q)/(1/h.q - 1))) âŠ” ((Î· 0)^2)
  use NNReal.sqrt K

  intros x hx
  rcases hx with âŸ¨n,hnâŸ©
  rw [â† hn]

  apply NNReal.le_sqrt_iff_sq_le.mpr
  by_cases hn : n = 0
  case pos =>
    unfold K
    rw [hn]
    apply le_max_right
  case neg =>
    have : n-1+1 = n := Nat.succ_pred_eq_of_ne_zero hn
    calc (Î· n)^2
      _ = (Î· ((n-1)+1))^2 := by rw [this]
      _ â‰¤ h.upperBound (n-1) := by exact estimator_recursive_upper_bound h (n-1)
      _ = h.q^n * (Î· 0)^2 + h.C * h.weightedSum (n-1) := by {unfold upperBound; simp [this]}
      _ â‰¤ h.q^n * (Î· 0)^2 + h.C * ((â¨† i, d i)^2 * (1/h.q)/(1/h.q - 1)) := by rel [weighted_sum_bound h hd (n-1)]
      _ â‰¤ (Î· 0)^2 + h.C * ((â¨† i, d i)^2 * (1/h.q)/(1/h.q - 1)) := by {
        gcongr
        by_cases hÎ· : (Î· 0)^2 = 0
        case pos =>
          simp [hÎ·]
        case neg =>
          have : h.q^n â‰¤ 1 := pow_le_one' (le_of_lt h.q_range.2) n
          rw [â† mul_le_mul_right (pos_of_ne_zero hÎ·)] at this
          simpa using this
      }
      _ â‰¤ K := by unfold K; apply le_max_left
}

lemma estimator_limsup_zero (hd : Tendsto d atTop (ğ“ 0)) (hÎ·â‚ : BddAbove (Set.range Î·)) : limsup (Î·^2) atTop = 0 := by {
  let âŸ¨q, q_range, C, C_pos, boundâŸ© := h

  apply smaller_q_eq_zero _ q q_range.2

  have hdc : Tendsto (C â€¢ d^2) atTop (ğ“ 0) := by {
    let f : NNReal â†’ NNReal := fun x â†¦ C * x^2
    have hf : Continuous f := by continuity
    have hf0 : f 0 = 0 := by unfold f; simp

    change Tendsto (f âˆ˜ d) atTop (ğ“ 0)
    rw [â† hf0]
    exact Tendsto.comp (hf.tendsto 0) hd
  }

  have hÎ·â‚‚ : BddAbove (Set.range (Î·^2)) := by {
    let f : NNReal â†’ NNReal := fun x â†¦ x^2
    change BddAbove (Set.range (f âˆ˜ Î·))
    apply monotone_map_bdd_above_range (pow_left_mono 2) hÎ·â‚
  }

  have hÎ·â‚ƒ : BddAbove (Set.range (q â€¢ Î·^2)) := by {
    let f : NNReal â†’ NNReal := fun x â†¦ q * x
    change BddAbove (Set.range (f âˆ˜ (Î·^2)))
    apply monotone_map_bdd_above_range mul_left_mono hÎ·â‚‚
  }

  have hâ‚ : limsup ((Î·^2) âˆ˜ (Â· + 1)) atTop â‰¤ limsup (q â€¢ Î·^2 + C â€¢ d^2) atTop := by {
    apply Filter.limsup_le_limsup
    Â· apply Filter.Eventually.of_forall
      intros x
      simp
      apply (bound x)
    Â· apply Filter.IsBoundedUnder.isCoboundedUnder_le
      apply BddBelow.isBoundedUnder_of_range
      apply nnreal_fun_bbd_below
    Â· apply BddAbove.isBoundedUnder_of_range
      apply BddAbove.range_add hÎ·â‚ƒ <| Tendsto.bddAbove_range hdc
  }

  -- TODO: write these anonymous functions with pointwise operations
  have hâ‚‚ : limsup (q â€¢ Î·^2 + C â€¢ d^2) atTop â‰¤ limsup (q â€¢ Î·^2) atTop + limsup (C â€¢ d^2) atTop := by {
    rw [â† NNReal.coe_le_coe]
    push_cast
    simp_rw [â† NNReal.toReal_limsup]

    apply limsup_add_le ?cÎ·_below ?cÎ·_above ?cd_below ?cd_above
    case cÎ·_below =>
      exact BddBelow.isBoundedUnder_of_range <| lift_bound_below _
    case cÎ·_above =>
      exact BddAbove.isBoundedUnder_of_range <| lift_bound_above _ hÎ·â‚ƒ
    case cd_below =>
      exact Filter.IsBoundedUnder.isCoboundedUnder_le <| BddBelow.isBoundedUnder_of_range <| lift_bound_below _
    case cd_above =>
      exact BddAbove.isBoundedUnder_of_range <| lift_bound_above _ <| Tendsto.bddAbove_range hdc
  }

  calc limsup (Î·^2) atTop
    _ = limsup (Î» n â†¦ (Î· (n+1))^2) atTop := by rw [â† Filter.limsup_nat_add _ 1]; rfl
    _ = limsup ((Î·^2) âˆ˜ (Â· + 1)) atTop := by rfl
    _ â‰¤ limsup (q â€¢ Î·^2 + C â€¢ d^2) atTop := by exact hâ‚
    _ â‰¤ limsup (q â€¢ Î·^2) atTop + limsup (C â€¢ d^2) atTop := by exact hâ‚‚
    _ = limsup (q â€¢ Î·^2) atTop := by simp [Tendsto.limsup_eq hdc]
    _ = q * limsup (Î·^2) atTop := by exact nnreal_limsup_const_mul <| BddAbove.isBoundedUnder_of_range hÎ·â‚‚
    _ = q * limsup (Î·^2) atTop := by rfl
}

theorem convergence_of_estimator_simple (hd_lim : Tendsto d atTop (ğ“ 0)) : Tendsto (Î·^2) atTop (ğ“ 0) := by {
  let hd_above := Tendsto.bddAbove_range hd_lim
  let hÎ·_above := estimator_bounded h hd_above
  have hÎ·2_above : BddAbove (Set.range (Î·^2)) := by {
    let f : NNReal â†’ NNReal := fun x â†¦ x^2
    change BddAbove (Set.range (f âˆ˜ Î·))
    apply monotone_map_bdd_above_range (pow_left_mono 2)
    exact hÎ·_above
  }
  have hÎ·2_below : BddBelow (Set.range (Î·^2)) := by exact nnreal_fun_bbd_below _
  let hÎ·_limsup := estimator_limsup_zero h hd_lim hÎ·_above

  apply tendsto_of_liminf_eq_limsup
  case hinf =>
    apply nonpos_iff_eq_zero.mp
    rw [â† hÎ·_limsup]
    apply liminf_le_limsup
    Â· exact BddAbove.isBoundedUnder_of_range hÎ·2_above
    Â· exact BddBelow.isBoundedUnder_of_range hÎ·2_below
  case hsup => exact hÎ·_limsup
  case h => exact BddAbove.isBoundedUnder_of_range hÎ·2_above
  case h' => exact BddBelow.isBoundedUnder_of_range hÎ·2_below
}

-- TODO real estimator reduction
end SimpleEstimatorReduction

variable {Î± Î² : Type*} [DecidableEq Î±] [Lattice Î±] [OrderBot Î±] (alg : AdaptiveAlgorithm Î± Î²)

-- TODO Feischl: Which limit is meant in the a priori convergence and
-- how does the convergence of this d_seq to zero follow from that?
def d_seq n := alg.d (alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ n)

-- TODO move all theorems about the algorithm into an algorithm namespace so that they
-- can be accessed with dot notation on the algorithm
theorem convergence_of_estimator (hd_seq_lim : Tendsto (d_seq alg) atTop (ğ“ 0)) :
    Tendsto alg.glob_err_nat atTop (ğ“ 0) := by {

  -- first define the object we want to apply the simplified convergence
  -- theorem to
  rcases alg.estimator_reduction_delta_exists with âŸ¨Î´, hÎ´, âŸ¨hÏ_est, hC_estâŸ©âŸ©

  let Ï_est := alg.Ï_est Î´
  let C_est := alg.C_est Î´

  have estimator_reduction := alg.estimator_reduction Î´ hÎ´ hÏ_est.2

  let d n := (d_seq alg n).toNNReal

  let est_red := {
    q := Ï_est.toNNReal,
    C := C_est.toNNReal,
    C_pos := by simpa using hC_est
    q_range := by simpa using hÏ_est
    bound := by {
      intros n
      apply NNReal.coe_le_coe.mp
      push_cast

      have hd : d n = d_seq alg n := by {
        apply Real.coe_toNNReal
        apply alg.non_neg
      }

      have hq : Ï_est.toNNReal = Ï_est := by {
        apply Real.coe_toNNReal
        exact le_of_lt hÏ_est.1
      }

      have hC : C_est.toNNReal = C_est := by {
        apply Real.coe_toNNReal
        exact le_of_lt hC_est
      }

      simp only [alg.hgÎ·, hd, hq, hC]
      unfold d_seq
      exact estimator_reduction n
    }
  : SimpleEstimatorReduction alg.gÎ· d}

  have hd_lim : Tendsto d atTop (ğ“ 0) := by {
    rw [Eq.symm Real.toNNReal_zero]
    apply tendsto_real_toNNReal hd_seq_lim
  }

  conv =>
    enter [1, n]
    rw [â† alg.hgÎ· n]
    norm_cast
  rw [â† NNReal.coe_zero]
  apply NNReal.tendsto_coe.mpr
  exact est_red.convergence_of_estimator_simple hd_lim
}

lemma cancel {Î´ a} (hÎ´ : Î´ > 0) : a * (alg.C_rel^2 * alg.C_est Î´ / (alg.C_rel^2 * alg.C_est Î´)) = a := by {
  apply mul_right_eq_selfâ‚€.mpr
  left
  apply EuclideanDomain.div_self
  apply ne_of_gt
  exact alg.C_rel_mul_C_est_pos hÎ´
}

-- Lemma 4.10
theorem summability : uniform_summability alg.gÎ· := by {
  rcases alg.Îµ_qo_lt_est_consts with âŸ¨Î´, hÎ´, hÎµ_qo, hÏ_estâŸ©
  -- TODO clean up the lt_est_consts lemma !!

  let v := alg.Îµ_qo * alg.C_rel^2 * alg.C_est Î´
  have hvâ‚ : v < 1 - alg.Ï_est Î´ := by {
    calc v
      _ = alg.Îµ_qo * alg.C_rel^2 * alg.C_est Î´ := by rfl
      _ < (1 - alg.Ï_est Î´) / (alg.C_rel^2 * alg.C_est Î´) * alg.C_rel^2 * alg.C_est Î´ := by {
        gcongr
        Â· exact alg.C_est_pos hÎ´
        Â· exact pow_pos alg.hC_rel 2
      }
      _ = (1 - alg.Ï_est Î´) * (alg.C_rel^2 * alg.C_est Î´ / (alg.C_rel^2 * alg.C_est Î´)) := by {
        field_simp
        rw [mul_assoc]
      }
      _ = 1 - alg.Ï_est Î´ := by {
        exact cancel alg hÎ´
      }
  }
  have hvâ‚‚ : 0 â‰¤ v := by {
    simp [v, mul_assoc]
    apply Left.mul_nonneg alg.hÎµ_qo.1
    exact le_of_lt <| alg.C_rel_mul_C_est_pos hÎ´
  }

  have : âˆ€ N l:â„•, âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1) â‰¤ âˆ‘ k âˆˆ range N, (alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * alg.C_qo * glob_err alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) := by {
    intros N l
    calc âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1)
      _ â‰¤ âˆ‘ k âˆˆ range N, (alg.Ï_est Î´ * alg.glob_err_nat (k + l) + alg.C_est Î´ * d_seq alg (k + l)^2) := by {
        gcongr with k hk
        exact alg.estimator_reduction Î´ hÎ´ hÏ_est (k+l)
      }
      _ = âˆ‘ k âˆˆ range N, ((alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * (d_seq alg (k + l)^2 - v * (alg.C_est Î´)â»Â¹ * alg.glob_err_nat (k + l))) := by {
        congr
        funext k
        rw [add_mul, mul_sub]
        conv in _ - _ =>
          rhs
          rw [â† mul_assoc]
          lhs
          tactic =>
            calc alg.C_est Î´ * (v * (alg.C_est Î´)â»Â¹)
              _ = (alg.C_est Î´ * (alg.C_est Î´)â»Â¹) * v := by ring
              _ = v := by rw [mul_inv_cancelâ‚€ <| ne_of_gt <| alg.C_est_pos hÎ´, one_mul]

        ring
      }
      _ â‰¤ âˆ‘ k âˆˆ range N, ((alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * (d_seq alg (k + l)^2 - v * (alg.C_est Î´)â»Â¹ * (alg.C_relâ»Â¹ * alg.d (alg.ğ’¯ <| k + l) alg.u (alg.U <| alg.ğ’¯ <| k + l))^2)) := by {
        gcongr with k hk
        Â· exact le_of_lt <| alg.C_est_pos hÎ´
        Â· refine mul_nonneg hvâ‚‚ ?_
          exact inv_nonneg.mpr <| le_of_lt <| alg.C_est_pos hÎ´
        Â· rw [mul_pow]
          calc alg.C_relâ»Â¹ ^ 2 * alg.d (alg.ğ’¯ (k + l)) alg.u (alg.U (alg.ğ’¯ (k + l))) ^ 2
            _ â‰¤ alg.C_relâ»Â¹ ^ 2 * (alg.C_rel ^ 2 * alg.glob_err_nat (k + l)) := by {
              have := (sq_le_sqâ‚€ (alg.non_neg _ _ _) ?_).mpr (alg.reliability <| alg.ğ’¯ <| k + l)
              swap
              Â· apply mul_nonneg
                Â· exact le_of_lt <| alg.hC_rel
                Â· apply Real.sqrt_nonneg
              simp [mul_pow, Real.sq_sqrt (glob_err_nonneg _ _ _)] at this
              unfold AdaptiveAlgorithm.glob_err_nat
              rel [this]
            }
            _ = alg.glob_err_nat (k + l) := by {
              rw [â† mul_assoc, â† mul_pow, inv_mul_cancelâ‚€ <| ne_of_gt <| alg.hC_rel]
              simp
            }
      }
      _ = âˆ‘ k âˆˆ range N, ((alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * (d_seq alg (k + l)^2 - v / (alg.C_rel^2 * alg.C_est Î´) * (alg.d (alg.ğ’¯ <| k + l) alg.u (alg.U <| alg.ğ’¯ <| k + l))^2)) := by {
        field_simp
        rw [mul_comm]
      }
      _ = âˆ‘ k âˆˆ range N, ((alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * (d_seq alg (k + l)^2 - alg.Îµ_qo * alg.d (alg.ğ’¯ <| k + l) alg.u (alg.U <| alg.ğ’¯ <| k + l)^2)) := by {
        dsimp [v]
        rw [mul_assoc, EuclideanDomain.mul_div_assoc, cancel alg hÎ´]
        Â· exact dvd_of_eq rfl
      }
      _ = âˆ‘ k âˆˆ range N, (alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * âˆ‘ k âˆˆ range N, (d_seq alg (k + l)^2 - alg.Îµ_qo * alg.d (alg.ğ’¯ <| k + l) alg.u (alg.U <| alg.ğ’¯ <| k + l)^2) := by {
        rw [Finset.sum_add_distrib]
        conv =>
          lhs
          rhs
          rw [â† Finset.mul_sum]
      }
      _ â‰¤ âˆ‘ k âˆˆ range N, (alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l) + alg.C_est Î´ * alg.C_qo * glob_err alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) := by {
        unfold d_seq
        have := alg.a3 l N
        apply add_le_add (by simp)
        rw [mul_assoc]
        exact (mul_le_mul_left <| alg.C_est_pos hÎ´).mpr this
      }
  }

  have : âˆ€ N l:â„•, (1-(alg.Ï_est Î´ + v)) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1) â‰¤ (alg.C_est Î´ * alg.C_qo + alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
    intros N l
    calc (1-(alg.Ï_est Î´ + v)) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1)
      _ = (1-(alg.Ï_est Î´ + v)) * (âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1) + alg.glob_err_nat l - alg.glob_err_nat l) := by ring
      _ = (1-(alg.Ï_est Î´ + v)) * (âˆ‘ k âˆˆ range (N + 1), alg.glob_err_nat (k + l) - alg.glob_err_nat l) := by {
        congr
        rw [Finset.sum_range_succ']
        conv =>
          rhs
          congr
          Â· rhs
            intro k
            rw [Nat.add_right_comm]
          Â· simp
      }
      _ = (1-(alg.Ï_est Î´ + v)) * âˆ‘ k âˆˆ range (N + 1), alg.glob_err_nat (k + l) - (1-(alg.Ï_est Î´ + v)) * alg.glob_err_nat l := by ring
      _ = (1-(alg.Ï_est Î´ + v)) * (âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) + alg.glob_err_nat (N + l)) - (1-(alg.Ï_est Î´ + v)) * alg.glob_err_nat l := by {
        rw [Finset.sum_range_succ]
      }
      _ â‰¤ (1-(alg.Ï_est Î´ + v)) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) + alg.glob_err_nat (N + l) - (1-(alg.Ï_est Î´ + v)) * alg.glob_err_nat l := by {
        rw [mul_add]
        gcongr
        apply mul_le_of_le_one_left
        Â· exact alg.glob_err_nat_nonneg _
        Â· rw [â† sub_sub]
          linarith [hvâ‚, hvâ‚‚, alg.Ï_est_pos hÎ´]
      }
      _ = âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) - (alg.Ï_est Î´ + v) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) + alg.glob_err_nat (N + l) - alg.glob_err_nat l + (alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        simp [sub_mul, one_mul, sub_add]
      }
      _ = âˆ‘ k âˆˆ range (N+1), alg.glob_err_nat (k + l) - (alg.Ï_est Î´ + v) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) - alg.glob_err_nat l + (alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        rw [Finset.sum_range_succ]
        ring
      }
      _ = âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1) - (alg.Ï_est Î´ + v) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l) + (alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        -- TODO this is the same as the second step without the factor in front
        rw [Finset.sum_range_succ']
        conv =>
          enter [1,1,1,1]
          congr
          Â· rhs
            intro k
            rw [Nat.add_right_comm]
          Â· simp
        ring
      }
      _ â‰¤ âˆ‘ k âˆˆ range N, (alg.Ï_est Î´ + v) * alg.glob_err_nat (k + l)
        + alg.C_est Î´ * alg.C_qo * glob_err alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l)
        - (alg.Ï_est Î´ + v) * âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l)
        + (alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        rel [this N l]
      }
      _ = alg.C_est Î´ * alg.C_qo * glob_err alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) + (alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        rw [Finset.mul_sum]
        ring
      }
      _ = (alg.C_est Î´ * alg.C_qo + alg.Ï_est Î´ + v) * alg.glob_err_nat l := by {
        unfold AdaptiveAlgorithm.glob_err_nat
        ring
      }
  }

  let C := (alg.C_est Î´ * alg.C_qo + alg.Ï_est Î´ + v)/(1-(alg.Ï_est Î´ + v))

  have key : âˆ€ N l:â„•, âˆ‘ k âˆˆ range N, alg.glob_err_nat (k + l + 1) â‰¤ C * alg.glob_err_nat l := by {
    intros N l
    unfold C
    rw [div_mul_eq_mul_divâ‚€]
    apply (le_div_iffâ‚€ ?_).mpr
    Â· rw [mul_comm]
      apply this
    Â· linarith [hvâ‚]
  }

  have summable : Summable alg.glob_err_nat := by {
    apply (summable_nat_add_iff 1).mp
    apply summable_of_sum_range_le
    Â· intros n
      apply alg.glob_err_nat_nonneg

    have := fun N â†¦ key N 0
    simpa using this
  }

  constructor
  Â· rw [â† NNReal.summable_coe]
    conv =>
      arg 1
      intro n
      simp
      rw [alg.hgÎ· n]
    exact summable
  Â· have C_pos : C > 0 := by {
      refine (lt_div_iffâ‚€' ?_).mpr ?_
      Â· linarith [hvâ‚]
      Â· simp only [mul_zero]
        refine Left.add_pos_of_pos_of_nonneg ?_ hvâ‚‚
        refine add_pos ?_ <| alg.Ï_est_pos hÎ´
        apply mul_pos (alg.C_est_pos hÎ´)
        linarith [alg.hC_qo]
    }

    have C_cast : â†‘C.toNNReal = C := by {
      rw [Real.coe_toNNReal]
      exact le_of_lt C_pos
    }

    use C.toNNReal
    refine âŸ¨Real.toNNReal_pos.mpr C_pos, ?_âŸ©

    intros l
    apply NNReal.coe_le_coe.mp
    push_cast
    rw [C_cast]
    simp only [Pi.pow_apply, NNReal.coe_pow, alg.hgÎ· l]
    conv =>
      lhs
      arg 1
      intro k
      rw [alg.hgÎ· _]
    refine Real.tsum_le_of_sum_range_le ?_ fun n â†¦ key n l
    intros n
    apply alg.glob_err_nat_nonneg
}
