import Mathlib
open Filter
open TopologicalSpace
open BigOperators
open Finset
open scoped Topology

class Partitionable (Î± : Type _) [DecidableEq Î±] where
  part : Finset Î± â†’ Î± â†’ Prop
  self_part : âˆ€ t : Î±, part {t} t
  union_part :
    âˆ€ {s : Î±} (m : Finset Î±) (ms : Î± â†’ Finset Î±),
      (âˆ€ t âˆˆ m, part (ms t) t) âˆ§ part m s â†’ part (m.biUnion ms) s
  unique_part :
    âˆ€ {s : Î±} (p m : Finset Î±),
      p âŠ† m âˆ§ part p s â†’ p = {s}
  unique_element : âˆ€ (s t : Î±),
      part {s} t â†’ t = s

notation:50 ts " â‡’ " t => Partitionable.part ts t
abbrev Mesh (Î± : Type*) := Finset Î±

variable {Î±: Type*} [DecidableEq Î±] [Partitionable Î±]
instance Mesh.orderBot : OrderBot (Mesh Î±) := by
  infer_instance

def refines (A B : Mesh Î±) : Prop :=
  âˆ€ t âˆˆ B, âˆƒ ts âŠ† A, ts â‡’ t

theorem refines_trans (X Y Z : Mesh Î±) (hxy: refines X Y) (hyz: refines Y Z) :
    refines X Z := by {
  intros t ht
  rcases hyz t ht with âŸ¨S,hS,hUâŸ©
  choose f hf using fun t ht => hxy t (hS ht)

  -- trick: use empty set when element is not in S because biUnion does
  -- not supply membership proof
  let U := S.biUnion fun x =>
    if hx : x âˆˆ S then f x hx else âˆ…
  use U

  constructor
  Â· apply Finset.biUnion_subset.mpr
    exact fun _ hs â†¦ by simp [hs, hf]
  Â· apply Partitionable.union_part
    exact âŸ¨fun _ hs â†¦ by simp [hs, hf], hUâŸ©
}

lemma biunion_is_singleton {Î± Î² : Type*} [DecidableEq Î²] (f : Î± â†’ Finset Î²)
      (A : Finset Î±) (b : Î²) (h : A.biUnion f = {b}) :
      âˆƒ s âˆˆ A, f s = {b} := by {
    have hb : b âˆˆ A.biUnion f := by simp [h]
    rcases mem_biUnion.mp hb with âŸ¨s, hsA, hbsâŸ©
    have hsub : f s âŠ† {b} := fun x hx =>
    by simpa [h] using mem_biUnion.mpr âŸ¨s, hsA, hxâŸ©
    exact âŸ¨s, hsA, Finset.eq_singleton_iff_unique_mem.mpr
    âŸ¨hbs, fun x hx => mem_singleton.1 (hsub hx)âŸ©âŸ©
}

lemma refines_antisymm_subset (A B : Mesh Î±) (hAB: refines A B) (hBA: refines B A) :
    A âŠ† B := by {
  intros t htA
  -- TODO: deduplicate this construction!
  obtain âŸ¨ts, hts_part, hts_subâŸ© := hBA t htA
  choose f hf using fun t ht => hAB t (hts_part ht)
  let g := fun x =>
     if hx : x âˆˆ ts then f x hx else âˆ…
  let U := ts.biUnion g

  have hâ‚: U â‡’ t := by {
     apply Partitionable.union_part
     exact âŸ¨fun _ hs â†¦ by unfold g; simp [hs, hf], hts_subâŸ©
  }
  have hâ‚‚: U âŠ† A := by {
    apply Finset.biUnion_subset.mpr
    exact fun _ hs â†¦ by unfold g; simp [hs, hf]
  }
  have : U = {t} := Partitionable.unique_part U A âŸ¨hâ‚‚, hâ‚âŸ©
  have : âˆƒ (s:Î±) (h : s âˆˆ ts), f s h = {t} := by {
    obtain âŸ¨s,hs,hsfâŸ© :=  biunion_is_singleton g ts t this
    use s, hs
    unfold g at hsf
    simp [hs] at hsf
    simp [hsf]
  }
  rcases this with âŸ¨s, hs, hssâŸ©
  have : s = t := Partitionable.unique_element t s (by {
    simp [â† hss, (hf s hs).2]
  })
  subst this
  apply hts_part
  exact hs
}

theorem refines_antisymm (A B : Mesh Î±) (hAB: refines A B) (hBA: refines B A) :
    A = B := by {
  apply Subset.antisymm_iff.mpr
  exact âŸ¨refines_antisymm_subset A B hAB hBA, refines_antisymm_subset B A hBA hABâŸ©
}

instance : LE (Mesh Î±) := âŸ¨refinesâŸ©
instance : LT (Mesh Î±) := âŸ¨fun f g => f â‰¤ g âˆ§ f â‰  gâŸ©

instance Mesh.partialOrder : PartialOrder (Mesh Î±) where
  le := (Â· â‰¤ Â·)
  lt := (Â· < Â·)
  le_antisymm := refines_antisymm
  lt_iff_le_not_le a b := by
    constructor
    Â· intros h
      exact âŸ¨h.1, by
        by_contra hâ‚‚
        have : a = b âˆ§ Â¬ a = b := âŸ¨refines_antisymm a b h.1 hâ‚‚, h.2âŸ©
        exact (and_not_self_iff (a = b)).mp this
      âŸ©
    Â· intros h
      exact âŸ¨h.1, by
        by_contra hâ‚‚
        rw [â† hâ‚‚] at h
        exact (and_not_self_iff (a â‰¤ a)).mp h
      âŸ©
  le_refl _ t h := âŸ¨{t}, singleton_subset_iff.mpr h, Partitionable.self_part tâŸ©
  le_trans := refines_trans

abbrev RefinementIndicator (Î± : Type*) (Î² : Type*) := Mesh Î± â†’ Î² â†’ Î± â†’ â„
variable {Î² : Type*}

def glob_err (ri: RefinementIndicator Î± Î²) (triang: Mesh Î±) v :=
  âˆ‘ t âˆˆ triang, (ri triang v t)^2

-- TOOD maybe move constants to their own structure that is already available before
-- AdaptiveAlgorithm and only put the Props into the structure
private noncomputable def Îµ_qos' (Ï_red C_rel C_red C_stab Î¸ : â„) := â¨† Î´ > 0, (1-(1+Î´)*(1-(1-Ï_red)*Î¸)) / (C_rel^2 * (C_red + (1+Î´â»Â¹)*C_stab^2))
private def C_rel' (C_Î” C_drel : â„) := C_Î” * C_drel

-- TODO unify notation for meshes, triangles and vectors (how much special characters to use?)
structure AdaptiveAlgorithm where
  U : Mesh Î± â†’ Î²
  -- limit
  u : Î²
  -- error estimator
  Î· : RefinementIndicator Î± Î²
  hÎ· : Î· â‰¥ 0
  -- error measure
  d : Mesh Î± â†’ Î² â†’ Î² â†’ â„
  C_Î” : â„
  hC_Î” : 0 < C_Î”
  non_neg : âˆ€ T v w, d T v w â‰¥ 0
  quasi_symmetry : âˆ€ T v w, d T v w â‰¤ C_Î” * d T w v
  quasi_triangle_ineq : âˆ€ T v w y, C_Î”â»Â¹ * d T v y â‰¤ d T v w + d T w y
  -- TODO error measure on X âˆª X(T) ?
  -- compatibility: âˆ€ T v w, âˆ€ T' â‰¤ T, d T' v w = d T v w
  further_approximation : âˆ€ T, âˆ€ Îµ > 0, âˆƒ T' â‰¤ T, d T' u (U T') â‰¤ Îµ
  -- Triangulations
  ğ’¯ : â„• â†’ Mesh Î±
  hğ’¯ : âˆ€ l, ğ’¯ (Nat.succ l) â‰¤ ğ’¯ l
  -- DÃ¶rfler marking
  Î¸ : â„
  hÎ¸ : Î¸ âˆˆ Set.Ioc 0 1
  â„³ : â„• â†’ Mesh Î±
  hâ„³ : âˆ€ l,
    -- Doerfler marking (2.5)
    -- TODO this says that the set has minimal cardinality, should be weakened
    -- to almost minimal cardinality
    let doerfler triang := Î¸ * glob_err Î· (ğ’¯ l) (U <| ğ’¯ l) â‰¤ âˆ‘ t âˆˆ triang, Î· (ğ’¯ l) (U <| ğ’¯ l) t ^ 2
    â„³ l âŠ† (ğ’¯ l \ ğ’¯ (l+1)) âˆ§ doerfler (â„³ l) âˆ§ âˆ€ M' âŠ† ğ’¯ l, doerfler M' â†’ (â„³ l).card â‰¤ M'.card
  -- A1: stability on non-refined element domains
  C_stab : â„
  hC_stab : C_stab > 0
  a1 : âˆ€ T, âˆ€ T' â‰¤ T, âˆ€ S âŠ† T âˆ© T', âˆ€ v v',
    |âˆš(âˆ‘ t âˆˆ S, Î· T' v' t ^ 2) - âˆš(âˆ‘ t âˆˆ S, Î· T v t ^ 2)| â‰¤ C_stab * d T' v' v
  -- A2: reduction property on refined elements
  Ï_red : â„
  hÏ_red : Ï_red âˆˆ Set.Ioo 0 1
  C_red : â„
  hC_red : 0 < C_red
  a2 : âˆ€ T, âˆ€ T' â‰¤ T, âˆ‘ t âˆˆ T' \ T, Î· T' (U T') t ^ 2 â‰¤ Ï_red * âˆ‘ t âˆˆ T \ T', Î· T (U T) t ^ 2 + C_red * d T' (U T') (U T) ^ 2
  -- A4: reliability
  C_drel : â„
  hC_drel : 0 < C_drel
  -- TODO this should be a result from A4 and the compatibility condition of the measure d
  -- would already be nicer as a sorry theorem
  reliability : âˆ€ T, d T u (U T) â‰¤ C_rel' C_Î” C_drel * âˆš(glob_err Î· T (U T))
  -- A3: general quasi-orthogonality
  -- this is last so that all constants are already available
  Îµ_qo : â„
  hÎµ_qo : 0 â‰¤ Îµ_qo âˆ§ Îµ_qo < Îµ_qos' Ï_red (C_rel' C_Î” C_drel) C_red C_stab Î¸
  C_qo : â„
  hC_qo : C_qo â‰¥ 1
  -- n + 1 is the number of summands here, don't need N â‰¥ l from paper
  a3 : âˆ€ l n, âˆ‘ k âˆˆ range n, (d (ğ’¯ <| k + l + 1) (U <| ğ’¯ <| k + l + 1) (U <| ğ’¯ k) ^ 2 - Îµ_qo * d (ğ’¯ <| k + l) u (U <| ğ’¯ <| k + l) ^ 2) â‰¤ C_qo * glob_err Î· (ğ’¯ l) (U <| ğ’¯ l)

namespace AdaptiveAlgorithm

variable (alg : @AdaptiveAlgorithm Î± _ _ Î²)
include alg

def Ï_est_fun Î´ := (1+Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)
noncomputable def C_est_fun Î´ := alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2

-- definitions for general field access
def C_rel := C_rel' alg.C_Î” alg.C_drel
noncomputable def Îµ_qoss := Îµ_qos' alg.Ï_red alg.C_rel alg.C_red alg.C_stab

end AdaptiveAlgorithm

-- TODO make name better so that it is clear this is the Î·^2 from the paper
def glob_err_nat (alg : @AdaptiveAlgorithm Î± _ _ Î²) l := glob_err alg.Î· (alg.ğ’¯ <| l) (alg.U <| alg.ğ’¯ <| l)

omit [DecidableEq Î±] [Partitionable Î±] in
theorem glob_err_nonneg (ri: RefinementIndicator Î± Î²) (triang: Mesh Î±) v : 0 â‰¤ glob_err ri triang v := by {
  apply sum_nonneg
  exact fun _ _ â†¦ sq_nonneg _
}

theorem glob_err_nat_nonneg (alg : @AdaptiveAlgorithm Î± _ _ Î²) :
  0 â‰¤ glob_err_nat alg := by {
    intros l
    -- example where simp alone does not work without
    -- specifying a closing theorem to use
    simpa using glob_err_nonneg _ _ _
}

theorem C_rel_pos (alg : @AdaptiveAlgorithm Î± _ _ Î²): 0 < alg.C_rel := by {
  exact mul_pos alg.hC_Î” alg.hC_drel
}

structure EstConst where
  Ï_est : â„
  hÏ_est : Ï_est âˆˆ Set.Ioo 0 1
  C_est : â„
  hC_est : 0 < C_est

def EstimatorReduction (alg : @AdaptiveAlgorithm Î± _ _ Î²) (c : EstConst) Î´ := c.Ï_est = alg.Ï_est_fun Î´ âˆ§ c.C_est = alg.C_est_fun Î´ âˆ§ âˆ€ l, glob_err_nat alg (l + 1) â‰¤ c.Ï_est * glob_err_nat alg l + c.C_est * alg.d (alg.ğ’¯ <| l + 1) (alg.U <| alg.ğ’¯ <| l+1) (alg.U <| alg.ğ’¯ <| l) ^ 2

-- Start of lemma 4.7
-- TODO move to file

lemma square_estimate_of_small_distance {a b c : â„} (ha : 0 â‰¤ a) (h : |a-b| â‰¤ c) :
  a^2 â‰¤ (b+c)^2 := by {
  have : a - b â‰¤ c := le_of_max_le_left h
  have : a â‰¤ b + c := tsub_le_iff_left.mp this
  exact pow_le_pow_leftâ‚€ ha this 2
}

example : 2^(1/2) = 1 := rfl

lemma young_with_delta {a b Î´ p q : â„} (ha : 0 â‰¤ a)  (hb : 0 â‰¤ b) (hÎ´ : 0 < Î´) (hpq : p.HolderConjugate q): a*b â‰¤ Î´/p * a^p + 1/(q*Î´^(q/p)) * b^q := by {
  have hÎ´â‚‚ := le_of_lt hÎ´
  have hpow_nonneg x := (Real.rpow_nonneg hÎ´â‚‚ x)
  have haâ‚‚ : 0 â‰¤ a * Î´^(1/p) := mul_nonneg ha (hpow_nonneg _)
  have hbâ‚‚ : 0 â‰¤ b * 1/Î´^(1/p) := by apply mul_nonneg <;> simp [hb, ha, hpow_nonneg _]
  have := Real.young_inequality_of_nonneg haâ‚‚ hbâ‚‚ hpq

  calc a*b
    _ = a * b * (Î´ ^ pâ»Â¹ * (Î´ ^ pâ»Â¹)â»Â¹) := by field_simp
    _ = a * Î´ ^ (1 / p) * (b * 1 / Î´ ^ (1 / p)) := by ring_nf
    _ â‰¤ (a * Î´ ^ (1 / p)) ^ p / p + (b * 1 / Î´ ^ (1 / p)) ^ q / q := this
    _ = Î´/p * a^p + (b * 1 / Î´ ^ (1 / p)) ^ q / q := by {
      rw [Real.mul_rpow ha <| hpow_nonneg _, â†Real.rpow_mul hÎ´â‚‚]
      simp [inv_mul_cancelâ‚€ <| Real.HolderTriple.ne_zero hpq, mul_comm]
      ring
    }
    _ = Î´/p * a^p + 1/(q*Î´^(q/p)) * b^q := by {
      field_simp
      rw [Real.div_rpow hb <| hpow_nonneg _, â†Real.rpow_mul hÎ´â‚‚]
      ring_nf
    }
}

lemma sum_square_le_square_sum {a b : â„} (ha : 0 â‰¤ a) (hb : 0 â‰¤ b) :
    âˆ€ Î´ > 0, (a+b)^2 â‰¤ (1+Î´)*a^2 + (1+Î´â»Â¹)*b^2 := by {
  intros Î´ hÎ´
  have := young_with_delta ha hb hÎ´ Real.HolderConjugate.two_two
  calc (a + b) ^ 2
    _ = a^2 + 2*(a*b) + b^2 := by ring
    _ â‰¤ a^2 + 2*(Î´/2 * a^2 + 1/(2*Î´) * b^2) + b^2 := by simpa using this
    _ = (1+Î´)*a^2 + (1+Î´â»Â¹)*b^2 := by ring
}

lemma doerfler_for_refined_elements (alg : @AdaptiveAlgorithm Î± _ _ Î²) :
    âˆ€ l, alg.Î¸ * glob_err_nat alg l â‰¤ âˆ‘ t âˆˆ (alg.ğ’¯ l \ alg.ğ’¯ (l+1)), alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by {
  intros l
  calc alg.Î¸ * glob_err_nat alg l
    _ â‰¤ âˆ‘ t âˆˆ alg.â„³ l, alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by exact (alg.hâ„³ l).2.1
    _ â‰¤ âˆ‘ t âˆˆ (alg.ğ’¯ l \ alg.ğ’¯ (l+1)), alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by {
      apply Finset.sum_le_sum_of_subset_of_nonneg
      Â· exact (alg.hâ„³ l).1
      Â· exact fun _ _ _ â†¦ sq_nonneg _
    }
}

-- Lemma 4.7
theorem adaptive_alg_estimator_reduction (alg : @AdaptiveAlgorithm Î± _ _ Î²) : âˆƒ c Î´, EstimatorReduction alg c Î´ := by {
  -- TODO this is alg.Ï_est_fun. refactor mono results etc out of here
  let h := fun Î´ â†¦ (1+Î´) * (1 - (1-alg.Ï_red) * alg.Î¸)
  let Î´ := 1/2 * ((1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹)

  -- h is linear, positive rate is the key to monotonicity
  have key : 0 < 1 - (1 - alg.Ï_red) * alg.Î¸ := by {
    field_simp
    apply mul_lt_one_of_nonneg_of_lt_one_left
    Â· simpa using le_of_lt alg.hÏ_red.2
    Â· simpa using alg.hÏ_red.1
    Â· exact alg.hÎ¸.2
  }
  have h_mono : StrictMono h := by {
    intros a b hab
    unfold h
    rel [key, hab]
  }
  -- 2*delta is positive
  have hÎ´_pre_pos : 0 < (1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹ := by {
    apply mul_pos _ (inv_pos.mpr key)
    simp [sub_mul]
    exact mul_lt_of_lt_one_left alg.hÎ¸.1 alg.hÏ_red.2
  }
  have hÎ´ : 0 < Î´ := by {unfold Î´; simp [hÎ´_pre_pos]}
  -- TODO: when working with Set.Ioo 0 1 so much, maybe it is worth it to add
  -- a type for this interval that has simp theorems for operations that
  -- stay inside the interval. for example 1/2 * x or 1 - x.

  use {
    Ï_est := h Î´
    hÏ_est := by {
      constructor
      Â· calc h Î´
          _ > h 0 := h_mono hÎ´
          _ > 0 := by {
            unfold h
            simp [key]
          }
      Â· calc h Î´
          _ < h ((1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹) := by {
            apply h_mono
            unfold Î´
            -- TODO this might be a good time for the mode where you can cursor around the expression
            -- giving this long argument to one_mul for it to recognise the right place is
            -- not very nice
            rw [one_div, â† one_mul ((1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹)]
            apply mul_lt_mul two_inv_lt_one <;> simp [hÎ´_pre_pos]
          }
          _ = 1 := by {
            unfold h
            rw [add_mul, mul_assoc, inv_mul_cancelâ‚€ <| Ne.symm (ne_of_lt key)]
            ring
          }
    }
    C_est := alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2
    hC_est := by {
      apply Left.add_pos_of_pos_of_nonneg alg.hC_red
      apply mul_nonneg _ (sq_nonneg _)
      apply add_nonneg (zero_le_one' â„)
      apply inv_nonneg.mpr
      exact le_of_lt hÎ´
    }
  }
  use Î´

  -- example where refine is a perfect match instead of apply
  refine âŸ¨by rfl, by rfl, ?_âŸ©

  intros l
  let summand n t := alg.Î· (alg.ğ’¯ n) (alg.U <| alg.ğ’¯ <| n) t ^ 2
  let distance n := alg.d (alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ <| n) ^ 2

  calc glob_err_nat alg (l + 1)
    _ = âˆ‘ t âˆˆ alg.ğ’¯ (l + 1) \ alg.ğ’¯ l, summand (l+1) t + âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1), summand (l+1) t := by {
      unfold glob_err_nat glob_err
      have h_eq : alg.ğ’¯ (l + 1) = (alg.ğ’¯ (l + 1) \ alg.ğ’¯ l) âˆª (alg.ğ’¯ (l + 1) âˆ© alg.ğ’¯ l) := by {
        exact Eq.symm (sdiff_union_inter (alg.ğ’¯ (l + 1)) (alg.ğ’¯ l))
      }
      nth_rw 1 [h_eq]
      simp [sum_union (disjoint_sdiff_inter _ _)]
      nth_rw 1 [inter_comm]
    }
    _ â‰¤ alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l + 1), summand l t + alg.C_red * distance l + (âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand (l + 1) t) := by rel[alg.a2 (alg.ğ’¯ l) (alg.ğ’¯ <| l + 1) (alg.hğ’¯ l)]
    _ â‰¤ alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l + 1), summand l t + alg.C_red * distance l + ((1 + Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand l t + (1 + Î´â»Â¹) * (alg.C_stab ^ 2 * distance l)) := by {
      have := alg.a1 (alg.ğ’¯ l) (alg.ğ’¯ <| l + 1) (alg.hğ’¯ l) (alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1)) (by rfl) (alg.U <| alg.ğ’¯ <| l) (alg.U <| alg.ğ’¯ <| l + 1)
      have := square_estimate_of_small_distance (Real.sqrt_nonneg _) this
      have hâ‚ : 0 â‰¤ alg.C_stab * alg.d (alg.ğ’¯ (l + 1)) (alg.U (alg.ğ’¯ (l + 1))) (alg.U (alg.ğ’¯ l)) := by {
        apply mul_nonneg (le_of_lt alg.hC_stab)
        apply alg.non_neg
      }
      have := le_trans this <| sum_square_le_square_sum (Real.sqrt_nonneg _) hâ‚ Î´ hÎ´

      rw [Real.sq_sqrt, Real.sq_sqrt, mul_pow] at this
      change âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand (l + 1) t â‰¤ (1 + Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand l t + (1 + Î´â»Â¹) * (alg.C_stab ^ 2 * distance l) at this
      rel [this]
      all_goals apply_rules [sum_nonneg', fun _ â†¦ sq_nonneg _]
    }
    _ = alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t + (1+Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1), summand l t + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
    _ = alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t + (1+Î´) * (glob_err_nat alg l -  âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t) + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      congr
      have h_eq : alg.ğ’¯ l = (alg.ğ’¯ l \ alg.ğ’¯ (l + 1)) âˆª (alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1)) := by exact Eq.symm (sdiff_union_inter (alg.ğ’¯ l) (alg.ğ’¯ (l + 1)))
      have h_dis: @Disjoint (Finset Î±) Finset.partialOrder Finset.instOrderBot (alg.ğ’¯ l \ alg.ğ’¯ (l + 1)) (alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1)) := by {
        exact disjoint_sdiff_inter (alg.ğ’¯ l) (alg.ğ’¯ (l + 1))
      }
      unfold glob_err_nat glob_err
      nth_rw 2 [h_eq]
      rw [sum_union (disjoint_sdiff_inter _  _)]
      ring
    }
    _ â‰¤ (1+Î´) * alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t + (1+Î´) * (glob_err_nat alg l - âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t) + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      gcongr
      refine (le_mul_iff_one_le_left ?_).mpr ?_
      Â· exact alg.hÏ_red.1
      Â· linarith
    }
    _ = (1+Î´) * (glob_err_nat alg l - (1-alg.Ï_red) * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t) + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
    _ â‰¤ (1+Î´) * (glob_err_nat alg l - (1-alg.Ï_red) * (alg.Î¸ * glob_err_nat alg l)) + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      have h : 0 â‰¤ 1 - alg.Ï_red := sub_nonneg_of_le <| le_of_lt alg.hÏ_red.2
      rel[doerfler_for_refined_elements alg l, h]
    }
    _ = (1+Î´) * (1 - (1-alg.Ï_red) * alg.Î¸) * glob_err_nat alg l + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
}
