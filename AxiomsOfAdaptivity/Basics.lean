import Mathlib
import AxiomsOfAdaptivity.Mesh
import AxiomsOfAdaptivity.Util

open Finset

variable {Î±: Type*} [DecidableEq Î±] [Lattice Î±] [OrderBot Î±]

-- ANCHOR: RefinementIndicator
abbrev RefinementIndicator (Î± : Type*) [DecidableEq Î±] [Lattice Î±] [OrderBot Î±] (Î² : Type*) :=
  Mesh Î± â†’ Î² â†’ Î± â†’ â„
-- ANCHOR_END: RefinementIndicator

-- ANCHOR: beta
variable {Î² : Type*}
-- ANCHOR_END: beta

-- ANCHOR: gÎ·2
def gÎ·2 (ri: RefinementIndicator Î± Î²) (triang: Mesh Î±) v :=
  âˆ‘ t âˆˆ triang, (ri triang v t)^2
-- ANCHOR_END: gÎ·2

theorem gÎ·2_nonneg (ri: RefinementIndicator Î± Î²) (triang: Mesh Î±) v : 0 â‰¤ gÎ·2 ri triang v := by {
  apply sum_nonneg
  exact fun _ _ â†¦ sq_nonneg _
}

/- This indexed supremum (iSup) looks like this after `dsimp [iSup]`, quite clever.
sSup
    (Set.range fun Î´ â†¦
      sSup
        (Set.range fun h â†¦
          (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2)))) -/
private noncomputable def Îµ_qos' (Ï_red C_rel C_red C_stab Î¸ : â„) := â¨† Î´ > 0, (1-(1+Î´)*(1-(1-Ï_red)*Î¸)) / (C_rel^2 * (C_red + (1+Î´â»Â¹)*C_stab^2))
private def C_rel' (C_Î” C_drel : â„) := C_Î” * C_drel

-- ANCHOR: AdaptiveAlgorithm
structure AdaptiveAlgorithm (Î± Î²: Type*) [DecidableEq Î±] [Lattice Î±] [OrderBot Î±] where
  -- Numerical solver --
  U : Mesh Î± â†’ Î²

  -- Limit --
  u : Î²

  -- Refinement indicator --
  Î· : RefinementIndicator Î± Î²
  hÎ· : Î· â‰¥ 0

  -- Error measure --
  d : Mesh Î± â†’ Î² â†’ Î² â†’ â„
  C_Î” : â„
  hC_Î” : 0 < C_Î”
  non_neg : âˆ€ T v w, d T v w â‰¥ 0
  quasi_symmetry : âˆ€ T v w, d T v w â‰¤ C_Î” * d T w v
  quasi_triangle_ineq : âˆ€ T v w y, C_Î”â»Â¹ * d T v y â‰¤ d T v w + d T w y
  -- Because we assume reliability directly compatibility is not used
  -- compatibility: âˆ€ T v w, âˆ€ T' â‰¤ T, d T' v w = d T v w
  further_approximation : âˆ€ T, âˆ€ Îµ > 0, âˆƒ T' â‰¤ T, d T' u (U T') â‰¤ Îµ

  -- Triangulation sequence --
  ğ’¯ : â„• â†’ Mesh Î±
  hğ’¯ : âˆ€ l, ğ’¯ (Nat.succ l) â‰¤ ğ’¯ l

  -- DÃ¶rfler marking --
  Î¸ : â„
  hÎ¸ : Î¸ âˆˆ Set.Ioc 0 1
  â„³ : â„• â†’ Mesh Î±
  -- Equation (2.5)
  -- Slightly stronger than AoA because it assumes the selected subset is
  -- of minimal instead of almost minimal cardinality
  hâ„³ : âˆ€ l,
    let doerfler M :=
      Î¸ * gÎ·2 Î· (ğ’¯ l) (U <| ğ’¯ l) â‰¤ âˆ‘ t âˆˆ M, Î· (ğ’¯ l) (U <| ğ’¯ l) t ^ 2
    â„³ l âŠ† (ğ’¯ l \ ğ’¯ (l+1))
    âˆ§ doerfler (â„³ l)
    âˆ§ âˆ€ M' âŠ† ğ’¯ l, doerfler M' â†’ (â„³ l).card â‰¤ M'.card

  -- A1: stability on non-refined element domains --
  C_stab : â„
  hC_stab : C_stab > 0
  a1 : âˆ€ T : Mesh Î±, âˆ€ T' â‰¤ T, âˆ€ S âŠ† T âˆ© T', âˆ€ v v',
    |âˆš(âˆ‘ t âˆˆ S, Î· T' v' t ^ 2) - âˆš(âˆ‘ t âˆˆ S, Î· T v t ^ 2)| â‰¤ C_stab * d T' v' v

  -- A2: reduction property on refined elements --
  Ï_red : â„
  hÏ_red : Ï_red âˆˆ Set.Ioo 0 1
  C_red : â„
  hC_red : 0 < C_red
  a2 : âˆ€ T : Mesh Î±, âˆ€ T' â‰¤ T,
    âˆ‘ t âˆˆ T' \ T, Î· T' (U T') t ^ 2 â‰¤ Ï_red * âˆ‘ t âˆˆ T \ T', Î· T (U T) t ^ 2 + C_red * d T' (U T') (U T) ^ 2

  -- A4: reliability --
  C_drel : â„
  hC_drel : 0 < C_drel
  -- This is a result from A4 and the compatibility condition of the measure d (Lemma 3.4).
  -- Because this proof is not formalized we assume this result instead of A4.
  reliability' : âˆ€ T, d T u (U T) â‰¤ C_rel' C_Î” C_drel * âˆš(gÎ·2 Î· T (U T))

  -- A3: general quasi-orthogonality --
  -- Comes last so that all constants are already available
  Îµ_qo : â„
  hÎµ_qo' : 0 â‰¤ Îµ_qo âˆ§ Îµ_qo < Îµ_qos' Ï_red (C_rel' C_Î” C_drel) C_red C_stab Î¸
  C_qo : â„
  hC_qo : C_qo â‰¥ 1
  -- Here n + 1 is the number of summands, we don't need N â‰¥ l from AoA
  a3 : âˆ€ l n,
    âˆ‘ k âˆˆ range n, (d (ğ’¯ <| k + l + 1) (U <| ğ’¯ <| k + l + 1) (U <| ğ’¯ <| k + l) ^ 2 - Îµ_qo * d (ğ’¯ <| k + l) u (U <| ğ’¯ <| k + l) ^ 2)
    â‰¤ C_qo * gÎ·2 Î· (ğ’¯ l) (U <| ğ’¯ l)
-- ANCHOR_END: AdaptiveAlgorithm

namespace AdaptiveAlgorithm

-- ANCHOR: alg
variable (alg : AdaptiveAlgorithm Î± Î²)
include alg
-- ANCHOR_END: alg

-- ANCHOR: lemma47_consts
def Ï_est Î´ := (1+Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)
noncomputable def C_est Î´ := alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2
-- ANCHOR_END: lemma47_consts

-- redefinitions for general field access
def C_rel := C_rel' alg.C_Î” alg.C_drel
noncomputable def Îµ_qos := Îµ_qos' alg.Ï_red alg.C_rel alg.C_red alg.C_stab alg.Î¸
lemma reliability : âˆ€ T, alg.d T alg.u (alg.U T) â‰¤ alg.C_rel * âˆš(gÎ·2 alg.Î· T (alg.U T)) := alg.reliability'

-- ANCHOR: seq_abbrev
def gÎ·2_seq l := gÎ·2 alg.Î· (alg.ğ’¯ <| l) (alg.U <| alg.ğ’¯ <| l)
noncomputable def nn_gÎ·_seq n := NNReal.sqrt (alg.gÎ·2_seq n).toNNReal
-- ANCHOR_END: seq_abbrev

-- lemmas for constants
lemma hÎµ_qo : 0 â‰¤ alg.Îµ_qo âˆ§ alg.Îµ_qo < alg.Îµ_qos := by {
  exact alg.hÎµ_qo'
}

lemma hC_rel : 0 < alg.C_rel := Left.mul_pos alg.hC_Î” alg.hC_drel

lemma C_est_pos {Î´} (hÎ´ : Î´ > 0) : 0 < alg.C_est Î´ := by {
  apply Left.add_pos_of_pos_of_nonneg alg.hC_red
  apply mul_nonneg _ (sq_nonneg _)
  apply add_nonneg (zero_le_one' â„)
  apply inv_nonneg.mpr
  exact le_of_lt hÎ´
}

lemma C_rel_mul_C_est_pos {Î´} (hÎ´ : Î´ > 0) : 0 < alg.C_rel ^ 2 * alg.C_est Î´ := by {
  apply mul_pos
  Â· exact pow_pos alg.hC_rel 2
  Â· exact alg.C_est_pos hÎ´
}

-- TODO This is absolutely illlegible
lemma Îµ_qo_lt_est_consts : âˆƒ Î´ > 0, alg.Îµ_qo < (1 - alg.Ï_est Î´) / (alg.C_rel^2 * alg.C_est Î´) âˆ§ alg.Ï_est Î´ < 1 := by {
  rcases @Real.add_neg_lt_sSup (Set.range fun Î´ â†¦ sSup (Set.range fun (h:Î´ > 0) â†¦ (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2)))) (by {
    apply Set.range_nonempty
  }) (alg.Îµ_qo - alg.Îµ_qos) (sub_neg.mpr alg.hÎµ_qo.2) with âŸ¨a, haâŸ©

  conv at ha =>
    rhs
    lhs
    lhs
    change alg.Îµ_qos

  rcases Set.mem_range.mp ha.1 with âŸ¨Î´, hÎ´âŸ©
  use Î´

  have : (Set.range fun (h:Î´ > 0) â†¦
      (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2))) â‰  âˆ… := by {
    by_contra h
    have : a = 0 := by {
      rw [â† hÎ´, h]
      exact Real.sSup_empty
    }
    have : Â¬ a = 0 := by {
      apply ne_of_gt
      linarith [ha.2, alg.hÎµ_qo]
    }
    contradiction
  }

  rcases Set.nonempty_iff_ne_empty.mpr this with âŸ¨b, hbâŸ©
  rcases Set.mem_range.mp hb with âŸ¨hÎ´', hbbâŸ©
  constructor
  Â· exact hÎ´'

  simp at ha

  have key : alg.Îµ_qo < (1 - alg.Ï_est Î´) / (alg.C_rel^2 * alg.C_est Î´) := by {
    unfold Ï_est C_est
    rw [hbb]
    have : Nonempty (Î´ > 0) := Nonempty.intro hÎ´'
    have : (Set.range fun (h:Î´ > 0) â†¦
        (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2))) = {(1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2))} := by {
      apply Set.range_const
    }
    have : a = b := by {
      calc a
        _ = sSup (Set.range fun h â†¦ (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2))) := by {
          rw [hÎ´]
        }
        _ = (1 - (1 + Î´) * (1 - (1 - alg.Ï_red) * alg.Î¸)) / (alg.C_rel ^ 2 * (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2)) := by {
          rw [this]
          apply csSup_singleton
        }
        _ = b := by {
          rw [hbb]
        }
    }
    rw [â† this]
    exact ha.2
  }

  constructor
  Â· unfold Ï_est C_est
    exact key
  Â· have : 0 < 1 - alg.Ï_est Î´ := by {
      have := by calc 0
        _ â‰¤ alg.Îµ_qo := alg.hÎµ_qo.1
        _ < (1 - alg.Ï_est Î´) / (alg.C_rel^2 * alg.C_est Î´) := key

      refine (div_pos_iff_of_pos_right ?_).mp this
      exact alg.C_rel_mul_C_est_pos hÎ´'
    }
    linarith
}

theorem gÎ·2_seq_nonneg :
  0 â‰¤ gÎ·2_seq alg := by {
    intros l
    -- example where simp alone does not work without
    -- specifying a closing theorem to use
    simpa using gÎ·2_nonneg _ _ _
}

lemma hnn_gÎ·_seq : âˆ€ n, alg.nn_gÎ·_seq n ^ 2 = alg.gÎ·2_seq n := by {
  intros n
  unfold nn_gÎ·_seq
  push_cast
  rw [Real.coe_toNNReal]
  apply Real.sq_sqrt
  all_goals exact alg.gÎ·2_seq_nonneg n
}

-- ANCHOR: doerfler_for_refined_elements
lemma doerfler_for_refined_elements :
    âˆ€ l, alg.Î¸ * gÎ·2_seq alg l
      â‰¤ âˆ‘ t âˆˆ (alg.ğ’¯ l \ alg.ğ’¯ (l+1)), alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by {
  intros l
  calc alg.Î¸ * gÎ·2_seq alg l
    _ â‰¤ âˆ‘ t âˆˆ alg.â„³ l, alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by exact (alg.hâ„³ l).2.1
    _ â‰¤ âˆ‘ t âˆˆ (alg.ğ’¯ l \ alg.ğ’¯ (l+1)), alg.Î· (alg.ğ’¯ l) (alg.U <| alg.ğ’¯ l) t ^ 2 := by {
      apply Finset.sum_le_sum_of_subset_of_nonneg
      Â· exact (alg.hâ„³ l).1
      Â· exact fun _ _ _ â†¦ sq_nonneg _
    }
}
-- ANCHOR_END: doerfler_for_refined_elements

-- Ï_est is linear, positive rate is the key to monotonicity
lemma Ï_est_pos_rate : 0 < 1 - (1 - alg.Ï_red) * alg.Î¸ := by {
  field_simp
  apply mul_lt_one_of_nonneg_of_lt_one_left
  Â· simpa using le_of_lt alg.hÏ_red.2
  Â· simpa using alg.hÏ_red.1
  Â· exact alg.hÎ¸.2
}

lemma Ï_est_strict_mono : StrictMono alg.Ï_est := by {
  intros a b hab
  unfold AdaptiveAlgorithm.Ï_est
  have := alg.Ï_est_pos_rate
  gcongr
}

lemma Ï_est_pos {Î´} (hÎ´ : Î´ > 0) : 0 < alg.Ï_est Î´ := by {
  calc alg.Ï_est Î´
    _ > alg.Ï_est 0 := alg.Ï_est_strict_mono hÎ´
    _ > 0 := by {
      unfold AdaptiveAlgorithm.Ï_est
      simp [alg.Ï_est_pos_rate]
    }
}

lemma estimator_reduction_delta_exists : âˆƒ Î´ > 0, alg.Ï_est Î´ âˆˆ Set.Ioo 0 1 âˆ§ 0 < alg.C_est Î´ := by {
  let Î´ := 1/2 * ((1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹)

  -- 2*delta is positive
  have hÎ´_pre_pos : 0 < (1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹ := by {
    apply mul_pos _ (inv_pos.mpr alg.Ï_est_pos_rate)
    simp [sub_mul]
    exact mul_lt_of_lt_one_left alg.hÎ¸.1 alg.hÏ_red.2
  }
  have hÎ´ : 0 < Î´ := by {unfold Î´; simp [hÎ´_pre_pos]}

  use Î´

  -- TODO: when working with Set.Ioo 0 1 so much, maybe it is worth it to add
  -- a type for this interval that has simp theorems for operations that
  -- stay inside the interval. for example 1/2 * x or 1 - x.

  -- example where refine is a perfect match instead of apply
  refine âŸ¨hÎ´, ?Ï_est_range, ?C_est_posâŸ©
  case Ï_est_range =>
    constructor
    Â· exact alg.Ï_est_pos hÎ´
    Â· calc alg.Ï_est Î´
        _ < alg.Ï_est ((1 - alg.Ï_red) * alg.Î¸ * (1 - (1 - alg.Ï_red) * alg.Î¸)â»Â¹) := by {
          apply alg.Ï_est_strict_mono
          apply mul_lt_of_lt_one_left hÎ´_pre_pos
          simp [two_inv_lt_one]
        }
        _ = 1 := by {
          unfold AdaptiveAlgorithm.Ï_est
          rw [add_mul, mul_assoc, inv_mul_cancelâ‚€ <| Ne.symm (ne_of_lt alg.Ï_est_pos_rate)]
          ring
        }
  case C_est_pos =>
    exact alg.C_est_pos hÎ´
}

-- Lemma 4.7
theorem estimator_reduction : âˆ€ Î´ > 0, (alg.Ï_est Î´ < 1) â†’
    âˆ€ l, alg.gÎ·2_seq (l + 1)
         â‰¤ alg.Ï_est Î´ * alg.gÎ·2_seq l
           + alg.C_est Î´ * alg.d (alg.ğ’¯ <| l + 1) (alg.U <| alg.ğ’¯ <| l+1) (alg.U <| alg.ğ’¯ <| l) ^ 2 := by {
  -- ANCHOR: estimator_reduction_1
  intros Î´ hÎ´ hÏ_est l

  let summand n t := alg.Î· (alg.ğ’¯ n) (alg.U <| alg.ğ’¯ <| n) t ^ 2
  let distance n := alg.d (alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ <| n + 1) (alg.U <| alg.ğ’¯ <| n) ^ 2
  -- ANCHOR_END: estimator_reduction_1

  -- ANCHOR: estimator_reduction_2
  calc gÎ·2_seq alg (l + 1)
    _ = âˆ‘ t âˆˆ alg.ğ’¯ (l + 1) \ alg.ğ’¯ l, summand (l+1) t
        + âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1), summand (l+1) t := by {
      unfold gÎ·2_seq gÎ·2
      have h_eq : (alg.ğ’¯ (l + 1)).val = (â†‘(alg.ğ’¯ (l + 1)) \ â†‘(alg.ğ’¯ l)) âˆª (â†‘(alg.ğ’¯ (l + 1)) âˆ© â†‘(alg.ğ’¯ l)) := by {
        exact Eq.symm (sdiff_union_inter _ _)
      }
      nth_rw 1 [h_eq]
      simp [sum_union (disjoint_sdiff_inter _ _)]
      nth_rw 1 [inter_comm]
    }
    -- ANCHOR_END: estimator_reduction_2
    -- ANCHOR: estimator_reduction_3
    _ â‰¤ alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l + 1), summand l t
        + alg.C_red * distance l
        + (âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand (l + 1) t) := by
      rel[alg.a2 (alg.ğ’¯ l) (alg.ğ’¯ <| l + 1) (alg.hğ’¯ l)]
    -- ANCHOR_END: estimator_reduction_3
    -- ANCHOR: estimator_reduction_4
    _ â‰¤ alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l + 1), summand l t
        + alg.C_red * distance l
        + ((1 + Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand l t
        + (1 + Î´â»Â¹) * (alg.C_stab ^ 2 * distance l)) := by {
      have := alg.a1
        (alg.ğ’¯ l)
        (alg.ğ’¯ <| l + 1)
        (alg.hğ’¯ l)
        (alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1))
        (fun _ a â†¦ a)
        (alg.U <| alg.ğ’¯ <| l)
        (alg.U <| alg.ğ’¯ <| l + 1)
      have := square_estimate_of_small_distance (Real.sqrt_nonneg _) this
      have hâ‚ : 0 â‰¤ alg.C_stab * alg.d (alg.ğ’¯ (l + 1)) (alg.U (alg.ğ’¯ (l + 1))) (alg.U (alg.ğ’¯ l)) := by {
        apply mul_nonneg (le_of_lt alg.hC_stab)
        apply alg.non_neg
      }
      have := le_trans this <| sum_square_le_square_sum (Real.sqrt_nonneg _) hâ‚ Î´ hÎ´

      rw [Real.sq_sqrt, Real.sq_sqrt, mul_pow] at this
      change âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand (l + 1) t â‰¤ (1 + Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l + 1), summand l t + (1 + Î´â»Â¹) * (alg.C_stab ^ 2 * distance l) at this
      rel [this]
      all_goals apply_rules [sum_nonneg', fun _ â†¦ sq_nonneg _]
    }
    -- ANCHOR_END: estimator_reduction_4
    -- ANCHOR: estimator_reduction_5
    _ = alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t
        + (1+Î´) * âˆ‘ t âˆˆ alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1), summand l t
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
    -- ANCHOR_END: estimator_reduction_5
    -- ANCHOR: estimator_reduction_6
    _ = alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t
        + (1+Î´) * (gÎ·2_seq alg l -  âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t)
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      congr
      have h_eq : (alg.ğ’¯ l).val = (â†‘(alg.ğ’¯ l) \ â†‘(alg.ğ’¯ (l + 1))) âˆª (â†‘(alg.ğ’¯ l) âˆ© â†‘(alg.ğ’¯ (l+1))) := by exact Eq.symm (sdiff_union_inter _ _)
      have h_dis: @Disjoint (Finset Î±) Finset.partialOrder Finset.instOrderBot (alg.ğ’¯ l \ alg.ğ’¯ (l + 1)) (alg.ğ’¯ l âˆ© alg.ğ’¯ (l+1)) := by {
        exact disjoint_sdiff_inter _ _
      }
      unfold gÎ·2_seq gÎ·2
      nth_rw 2 [h_eq]
      rw [sum_union (disjoint_sdiff_inter _  _)]
      ring
    }
    -- ANCHOR_END: estimator_reduction_6
    -- ANCHOR: estimator_reduction_7
    _ â‰¤ (1+Î´) * alg.Ï_red * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t
        + (1+Î´) * (gÎ·2_seq alg l - âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t)
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      gcongr
      refine (le_mul_iff_one_le_left ?_).mpr ?_
      Â· exact alg.hÏ_red.1
      Â· linarith
    }
    -- ANCHOR_END: estimator_reduction_7
    -- ANCHOR: estimator_reduction_8
    _ = (1+Î´) * (gÎ·2_seq alg l - (1-alg.Ï_red) * âˆ‘ t âˆˆ alg.ğ’¯ l \ alg.ğ’¯ (l+1), summand l t)
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
    _ â‰¤ (1+Î´) * (gÎ·2_seq alg l - (1-alg.Ï_red) * (alg.Î¸ * gÎ·2_seq alg l))
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by {
      have hâ‚ : 0 â‰¤ 1 - alg.Ï_red := sub_nonneg_of_le <| le_of_lt alg.hÏ_red.2
      rel[alg.doerfler_for_refined_elements l, hâ‚]
    }
    _ = (1+Î´) * (1 - (1-alg.Ï_red) * alg.Î¸) * gÎ·2_seq alg l
        + (alg.C_red + (1 + Î´â»Â¹) * alg.C_stab ^ 2) * distance l := by ring
    -- ANCHOR_END: estimator_reduction_8
}

end AdaptiveAlgorithm
